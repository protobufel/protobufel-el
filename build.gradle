buildscript {
    repositories {
        jcenter()
        mavenCentral()
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

plugins {
    id 'java-library'
    id 'maven'
    id 'signing'

    id 'io.codearte.nexus-staging' version "0.9.0"
    id 'net.researchgate.release' version '2.6.0'

    //id 'eclipse'
    id "me.champeau.gradle.jmh" version "0.4.2"

    id 'jacoco'
}

repositories {
    jcenter()
    mavenCentral()
}

//plugins.withType(EclipsePlugin) {
//  project.eclipse.classpath.plusConfigurations += [ configurations.jmh ]
//}

group = 'com.github.protobufel'
archivesBaseName = 'protobufel-el'
sourceCompatibility = 1.8
targetCompatibility = 1.8

def testGrammarProtobuf = "$buildDir/resources/test/com/github/protobufel"

configurations {
    shared
}

sourceSets {
    test {
        resources {
            output.dir(testGrammarProtobuf)
        }
    }
}

dependencies {
    //api '...'
    api 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.1.0'
    api 'javax.el:javax.el-api:3.0.1-b04'

    shared 'com.github.protobufel:protobufel-test-protos:0.7.1@jar'

    implementation 'org.glassfish:javax.el:3.0.1-b08'
    implementation 'com.github.protobufel:protobufel:0.6' //FIXME: update to latest!
    //implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '2.6.1' //FIXME: remove as transitive?

    testImplementation 'com.github.protobufel:protobufel-test-util:0.7.0'

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'com.google.guava', name: 'guava', version: '22.0'
    testImplementation 'com.google.guava:guava-testlib:22.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'

    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.0'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.0'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
}

task copyGrammarProtobuf(type: Sync, group: 'runtime-resource') {
    println "hi:" + configurations.shared.files.find({ file -> file.name.contains('protobufel-test-protos') }).name

    from zipTree(configurations.shared.files.find { file -> file.name.matches('.*protobufel-test-protos.*\\.jar') })
    include '**/*'
    exclude '**/META-INF/**/*'
    exclude '/META-INF'
    into testGrammarProtobuf
}

processTestResources.dependsOn copyGrammarProtobuf

if (project.hasProperty('ossrhUsername')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'protobufel-el'
                    packaging 'jar'
                    description 'ProtoBuf integration with EL 3.0'
                    url 'https://github.com/protobufel/protobufel-el'

                    scm {
                        connection 'scm:git:https://github.com/protobufel/protobufel-el.git'
                        developerConnection 'scm:git@github.com:protobufel/protobufel-el.git'
                        url 'https://github.com/protobufel/protobufel-el'
                    }

                    licenses {
                        license {
                            name 'The BSD 3-Clause License'
                            url 'http://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'protobufel'
                            name 'David Tesler'
                            email 'protobufel@gmail.com'
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        //packageGroup = "com.github.protobufel.multikeymapjava" //optional if packageGroup == project.getGroup()
        stagingProfileId = "com.github.protobufel" //when not defined will be got from server using "packageGroup"
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task copyJavadocToDoc(type: Sync, group: 'documentation') {
        from javadoc
        into("docs/javadoc")
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    preTagCommit.dependsOn copyJavadocToDoc

    release {
        preCommitText = ''
        preTagCommitMessage = 'pre tag commit: '
        tagCommitMessage = 'bumped to '
        newVersionCommitMessage = 'new version commit: '
//        tagTemplate = "${version}"
    }

    afterReleaseBuild.dependsOn uploadArchives
} else {
    test {
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "short"

            debug {
                events "skipped", "failed"
                exceptionFormat "full"
            }

            info.events = ["failed", "skipped"]
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

jmh {
    jmhVersion = '1.19' // Specifies JMH version
    includeTests = false
    // Allows to include test sources into generate JMH jar, i.e. use it when benchmarks depend on the test classes.
    duplicateClassesStrategy = 'FAIL'
    // Strategy to apply when encountring duplicate classes during creation of the fat jar (i.e. while executing jmhJar task)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }

        rule {
            enabled = false
            element = 'CLASS'
            includes = ['com.github.protobufel.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport